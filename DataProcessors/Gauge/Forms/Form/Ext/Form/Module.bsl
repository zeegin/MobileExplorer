
&AtServer
Procedure OnCreateAtServer(Cancel, StandardProcessing)
	
	СоздатьДиаграмму(Chart1, Metadata.StyleItems.UsualTextColor.Value);
	
	ЗаполнитьДиаграмму(Chart1, 100, 25);
	
EndProcedure

&AtClient
Procedure PercentOnChange(Item)
	
	PercentOnChangeAtServer();
	
EndProcedure

&AtServer
Procedure PercentOnChangeAtServer()
	
	ЗаполнитьДиаграмму(Chart1, 100, Percent);
	
EndProcedure

&НаСервере
Процедура СоздатьДиаграмму(Диаграмма, Цвет)
	
	Диаграмма.Обновление = Ложь;
	
	Диаграмма.Окантовка = Ложь;
	Диаграмма.ТипДиаграммы = ТипДиаграммы.Кольцевая;
	Диаграмма.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Нет;
	Диаграмма.ОбластьЗаголовка.Расположение = РасположениеОбластиЗаголовкаДиаграммы.УказываетсяРасположение;
	Диаграмма.ОбластьЗаголовка.ПрозрачныйФон = Истина;
	Диаграмма.ОбластьЗаголовка.Верх = 0.25;
	Диаграмма.ОбластьЗаголовка.Низ = 0.75;
	Диаграмма.ОбластьЗаголовка.Лево = 0.25;
	Диаграмма.ОбластьЗаголовка.Право = 0.75;
	
	Диаграмма.ОбластьЗаголовка.ЦветТекста = Цвет;
	Диаграмма.ОбластьЗаголовка.Шрифт = Новый Шрифт( , 24, Истина);
	
	Диаграмма.ФорматЗначенийВПодписях = "ЧФ = 'Ч '";
	
	Диаграмма.Точки.Добавить();
	Диаграмма.Точки[0].Текст = "";
	
	Диаграмма.Серии.Добавить("Используется");
	Диаграмма.Серии[0].Цвет = Цвет;
	
	Диаграмма.Серии.Добавить("Доступно");
	Диаграмма.Серии[1].Цвет = Новый Цвет(238, 238, 238);
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграмму(Диаграмма, Всего, Использовано)
	
	Диаграмма.Обновление = Ложь;
	
	Диаграмма.ОбластьЗаголовка.Текст = Строка(Окр(Использовано / Всего * 100)) + "%";
	
	Диаграмма.УстановитьЗначение(0, 0, Использовано);
	Диаграмма.УстановитьЗначение(0, 1, Всего - Использовано);
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры
